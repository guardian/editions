// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "31.0.0"
        minSdkVersion = 21
        compileSdkVersion = 31
        targetSdkVersion = 31
        supportLibVersion = '1.0.2' // Use '28.0.0' or don't specify for old libraries, '1.0.2' or similar for AndroidX
        androidXVersion = '1.0.2' // This is for react-native-inappbrowser-reborn
        mediaCompatVersion = '1.0.1' // Do not specify if using old libraries, specify '1.0.1' or similar for androidx.media:media dependency
        supportV4Version = '1.0.0' // Do not specify if using old libraries, specify '1.0.0' or similar for androidx.legacy:legacy-support-v4 dependency
        androidXAnnotation = "1.1.0"
        androidXBrowser = "1.0.0"
        if (System.properties['os.arch'] == "aarch64") {
            // For M1 Users we need to use the NDK 24 which added support for aarch64
            ndkVersion = "24.0.8215888"
        } else {
            // Otherwise we default to the side-by-side NDK version from AGP.
            ndkVersion = "21.4.7075529"
        }
        FLIPPER_VERSION = "0.103.0"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:7.1.1")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("de.undercouch:gradle-download-task:5.0.1")
        
        classpath("com.google.gms:google-services:4.3.8")

        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.5.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenCentral {
            // We don't want to fetch react-native from Maven Central as there are
            // older versions over there.
            content {
                excludeGroup "com.facebook.react"
            }
        }
        google()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url "$rootDir/../node_modules/react-native/android"
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }
        maven {
           // react-native-background-fetch
           url("${project(':react-native-background-fetch').projectDir}/libs")
       }
       jcenter()
    }


    configurations.all {
            resolutionStrategy {
                force 'com.google.android.gms:play-services-gcm:16.1.0'
                dependencySubstitution {
                    substitute module("com.android.support:appcompat-v7:1.0.2") because "this is a simulcrum module because react-native-notifications isn't actively developed any more" with module("androidx.appcompat:appcompat:1.0.2")
                }
           }
    }
}
